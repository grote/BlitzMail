buildscript {
	ext.kotlin_version = '1.2.71'
	repositories {
		jcenter()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:3.2.0'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

allprojects {
	repositories {
		google()
		jcenter()
	}
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	implementation 'androidx.appcompat:appcompat:1.0.0'
	implementation 'com.sun.mail:android-mail:1.6.2'
	implementation 'com.sun.mail:android-activation:1.6.2'
}

android {
	defaultConfig {
		versionCode 13
		versionName "0.7.0"

		compileSdkVersion 28
		targetSdkVersion 28
		minSdkVersion 14
		buildToolsVersion "28.0.3"
	}

	flavorDimensions "version"
	productFlavors {
		normal {
			applicationId "de.grobox.blitzmail"
			buildConfigField "boolean", "PRO", "true"
			dimension "version"
		}

		pro {
			applicationId "de.grobox.blitzmail.pro"
			buildConfigField "boolean", "PRO", "true"
			dimension "version"
		}

		lite {
			applicationId "de.grobox.blitzmail.lite"
			buildConfigField "boolean", "PRO", "false"
			dimension "version"
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}
	}

	android.sourceSets.pro {
		res.srcDir 'flavors/pro/res'
	}

	/*
	* To sign release build, create file gradle.properties in ~/.gradle/ with this content:
	*
	* signingStoreLocation=/home/key.store
	* signingStorePassword=xxx
	* signingKeyAlias=alias
	* signingKeyPassword=xxx
	*/
	if (project.hasProperty('signingStoreLocation') &&
			project.hasProperty('signingStorePassword') &&
			project.hasProperty('signingKeyAlias') &&
			project.hasProperty('signingKeyPassword')) {
		println "Found sign properties in gradle.properties! Signing buildâ€¦"

		signingConfigs {
			release {
				storeFile file(signingStoreLocation)
				storePassword signingStorePassword
				keyAlias signingKeyAlias
				keyPassword signingKeyPassword
			}
		}

		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	// Do not abort build if lint finds errors
	lintOptions {
		abortOnError false
		disable 'MissingTranslation'
	}
}
